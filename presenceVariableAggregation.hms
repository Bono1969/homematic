! https://github.com/jollyjinx/homematic/blob/master/presenceVariableAggregation.hms
!
! Aggregates variables to ones that are used in scripts.
! I have multiple variables that detect presence:
!
! The aggregated variables are in the form:
!
! presence.any          is anybody home
! presence.guests       are guests at home
! presence.patrick      i'm at home
! presence....
!
! Those variables are aggregated through variables named presence.type.personname
!
! presence.wlan.guests          someone is in the guest wlan
! presence.wlan.patrick         i'm logged in to the wlan
!
! presence.manual.guests        manually set via a virtual ccu2 switch
! presence.timetable.patrick    presence set via a timetable


!----- User setable variables

string  presenceprefix          = "presence";
string  allpersonspresencename  = "any";
boolean debug                   = true;

!------- nothing needs to be changed below.

string  allvariables        = ","#allpersonspresencename#",";
string  presencevariables    = ",";
boolean someoneispresent    = false;

string variableID;

foreach(variableID, dom.GetObject(ID_SYSTEM_VARIABLES).EnumUsedIDs()) 
{
    string  variablename    = dom.GetObject(variableID).Name();                     if(debug){WriteLine("Variable:"#variablename);}
    if( presenceprefix == variablename.StrValueByIndex(".",0) )
    {
        string  persontype      = variablename.StrValueByIndex(".",2);
        
        if( persontype.Length() > 0 )
        {
            if( -1 == allvariables.Find(","#persontype#",") )
            {
                allvariables = allvariables#persontype#",";                         if(debug){WriteLine("\tAdding to all:"#persontype);}
            }
            if( -1 == presencevariables.Find(","#persontype#",") )
            {           
                var variablevalue   = dom.GetObject(variablename).Value();          if(debug){WriteLine("\t"#variablename#":"#variablevalue);}
                
                if( true == variablevalue )
                {
                    if( !someoneispresent )
                    {
                        presencevariables = ","#allpersonspresencename#",";          if(debug){WriteLine("\tsomeoneispresent is present.");}
                        someoneispresent = true;
                    }
                    presencevariables = presencevariables#persontype#",";             if(debug){WriteLine("\tpresencevariables:"#presencevariables);}
                }   
            }
            else
            {
                                                                                    if(debug){WriteLine("\t"#persontype#" already present");}
            }
        }
    }
}


if(debug) { WriteLine("AllPersontypes:"#allvariables); }
if(debug) { WriteLine("Present:"#presencevariables); }


string persontype;

foreach(persontype,allvariables.Split(","))
{
    if( persontype.Length() > 0 )
    {
        boolean ispresent = false;
        
        if( -1 != presencevariables.Find(","#persontype#",") )
        {                                                                               if(debug){WriteLine("\tpersontype found:"#persontype);}
            ispresent = true;
        }
        
        var presencevariable = dom.GetObject("presence."#persontype);
        if( ispresent != presencevariable.Value() )
        {                                                                               if(debug){WriteLine("\tpresence."#persontype#" has changed:"#ispresent);}
            presencevariable.State(ispresent);
        }
        else
        {                                                                               if(debug){WriteLine("\tpresence."#persontype#" has not changed:"#ispresent);}

        }
    }
}
