!https://github.com/jollyjinx/homematic/blob/master/PresenceTriggersTemperature.hms
!
! This script changes the SET_TEMPERATURE of thermostats depending if a person is present
! in a room.
! If there are persons present in the house hand have a location/time/temperature table
! It uses the function/Gewerk 'Heating' to find out switched in that room to deduce presence of someone
! If there are openings in the 'Heating' function/Gewerk so not to heat
!

real    miniumroomtemp      = 10.0;
real    openwindowtemp      = 5.0;
real    compforttemp        = 20.0;
string  heatingfunctionname = "Heating";                    ! name of the function/"Gewerk" heating objects are put in
string  prefix              = "presence.";

! be aware to use spaces in the following strings before everything that's not a space
! otherwise it won't be recognized by a Split(" ")

string  persons             = "

    presence.guests:        hall        0:00,17C    6:00,18.5C  7:30,19.5C  19:30,20C   22:30,17C   
                            living      0:00,17C                7:30,19C    19:30,20C   22:30,17C   
                            kitchen     0:00,17C    6:00,18.5C  7:30,20C    20:30,17C               
                            guests      0:00,17C                8:00:16C    21:00,17.5C             

    presence.patrick:       hall        0:00,16C                7:30:18C    20:30,19C   22:00,17C   
                            living      0:00,16C                7:30:17C    20:30,18C   22:00,17C   
                            kitchen     0:00,16C                7:30,18C    20:30,17C               
                            office      0:00,16C                7:30,20C    19:30,16.5C             
                            sleeping    0:00,15C                8:00,10C    21:00,15C               

    presence.isabel:        hall        0:00,18C    6:00,18.5C  7:30,20.0C  20:00,17C   
                            living      0:00,18C                7:30,18.5C  20:00,17C   
                            kitchen     0:00,17C    6:00,18.5C  7:30,19.5C  20:00,17C               
                            isabel      0:00,17.5C              7:30,19.5C  19:30,17.5C             
 ";

boolean debug               = false;

! 
!--- no user setable parts below this point 
! 

integer	currenthour		= 	system.Date("%H").ToInteger();
integer	currentminute	= 	system.Date("%M").ToInteger();
integer timenow         =   (currenthour*3600)+(currentminute*60);					 if(debug){WriteLine("timenow:"#timenow);}
integer prefixlength    =   prefix.Length();
string  roomtemperatures;
integer index;

while( -1 != persons.Find(prefix) )
{
    integer index1      = persons.Find(prefix);
    integer offset1     = index1 + prefixlength;
    string  substring1  = persons.Substr(offset1,persons.Length()-offset1);
    string  substring2;
    
    if( -1 != substring1.Find(prefix) )
    {
        integer index2  = substring1.Find(prefix);
        substring2      = substring1.Substr(0,index2);
        persons         = substring1.Substr(index2,substring1.Length()-index2);
    }
    else
    {
        persons     = "";
        substring2  = substring1;
    }

    string  person              = substring2.StrValueByIndex(":",0);
    string  personpreferences   = substring2.Substr(person.Length(),substring2.Length()-person.Length());

    object  personvariable      = dom.GetObject(prefix#person);
    boolean personispresent     = personvariable.Value();
    boolean personwaspresent    = personvariable.LastValue();
    integer personchangedtime   = personvariable.Timestamp().ToInteger() % 86400;
    
    if(personispresent==personwaspresent)   { personchangedtime = -86400; }                                 if(debug){WriteLine("Person:"#person#" presence:"#personispresent#" since:"#personchangedtime#" waspresent:"#personwaspresent);}
        
    if( true == personispresent )
    {                                                                                                       if(debug){WriteLine("\n\t"#personpreferences);}
        string  currentroom;
        
        integer currentchangetime   = -1;
        real    currentchangetemp   = -1;
        integer lastchangetime      = -1;
        real    lastchangetemp      = 0;
        
        string  preferences;
        
        foreach(preferences,personpreferences.Split(" "))
        {
            if( preferences.Length() > 1 )
            {																					        	!if(debug){WriteLine("GotSplit:"#preferences);}
                if( (-1 == preferences.Find(",")) && (-1 == preferences.Find(":")) )
                {
                    if( currentroom.Length() )
                    {                                                                                       if(debug){WriteLine("\troom:"#currentroom#","#currentchangetime#","#currentchangetemp#","#lastchangetime#","#lastchangetemp);}
                        roomtemperatures = roomtemperatures#"\t"#currentroom#","#currentchangetime#","#currentchangetemp#","#lastchangetime#","#lastchangetemp;
                    }
                    currentroom = preferences;
                }
                else
                {
                    if( (-1 != preferences.Find(",")) && (-1 != preferences.Find(":")) ) 
                    {
                        string  timestring  =   preferences.StrValueByIndex(",",0);
                        integer timeseconds =       (3600*(timestring.StrValueByIndex(":",0).ToInteger()))
                                                 +  (  60*(timestring.StrValueByIndex(":",1).ToInteger()));
                        real    temperature =   preferences.StrValueByIndex(",",1).ToFloat();
                        																					        	if(debug){WriteLine("timestring:"#timestring#" timeseconds:"#timeseconds#" temperature:"#temperature);}
                        if( timeseconds <= timenow )
                        {
                            lastchangetime      = currentchangetime;
                            lastchangetemp      = currentchangetemp;
                            currentchangetime   = timeseconds;
                            currentchangetemp   = temperature;
                        }
                        else
                        {
                            if( lastchangetime < 0 )
                            {
                                lastchangetime      = timeseconds-86400;
                                lastchangetemp      = temperature;
                            }
                        }
                    }
                }
            }
        }
        if( currentroom.Length() )
        {                                                                                                   if(debug){WriteLine("\troom:"#currentroom#","#currentchangetime#","#currentchangetemp#","#lastchangetime#","#lastchangetemp);}
            roomtemperatures = roomtemperatures#"\t"#currentroom#","#currentchangetime#","#currentchangetemp#","#lastchangetime#","#lastchangetemp;
        }
    }
    else
    {
                                                                                                            if(debug){WriteLine("Person:"#person#" is absent");}
    }
}
    if(debug){WriteLine("\t"#roomtemperatures);}


string  roomid;
foreach( roomid, dom.GetObject(ID_ROOMS).EnumUsedIDs() )
{
    object  room                = dom.GetObject(roomid);                                                    if(debug){WriteLine("\n\nroom:"#room);}
    boolean openwindowordoor    = false;
    boolean somebodyispresent   = false;
    string  thermostatdevices;
    string  channelid;
    
    foreach(channelid,dom.GetObject(room).EnumUsedIDs())
    {                                                                                                       if(debug){WriteLine("\t channelid:"#channelid);}
        object  channel             = dom.GetObject(channelid);                                             if(debug){WriteLine("\t channel:"#channel);}
        string  functionID;
        
        foreach(functionID,channel.ChnFunction().EnumUsedIDs())
        {
            object function=dom.GetObject(functionID);                                                      
            
            if( heatingfunctionname == function.Name() )
            {
                object  device      = dom.GetObject(channel.Device());                                      if(debug){WriteLine("\t device:"#device);}
                string  devicehssid = device.HSSID();                                                       if(debug){WriteLine("\t devicehssid:"#devicehssid);}

                if( "HM-CC-RT-DN" == devicehssid )
                {
                        var interface=dom.GetObject(channel.Interface());
                        thermostatdevices = thermostatdevices # "\t" # interface # "." # channel.Address();
                }
                else
                {
                    if( !openwindowordoor )
                    {
                        string datapointid;
                        foreach(datapointid,channel.DPs().EnumUsedIDs())
                        {
                            var datapoint   = dom.GetObject(datapointid);

                            if(OPERATION_READ & datapoint.Operations())
                            {
                                string  dpname = datapoint.Name();

                                if( -1 != dpname.Find(".STATE") )
                                {                                                                       if(debug){WriteLine("\t Datapoint:"#dpname);}
                                    if( 0 == devicehssid.Find("HM-Sec-") )
                                    {                                                                   if(debug){WriteLine("\t WindowOrDoor: state:"#datapoint.Value());}
                                        if( datapoint.Value() )
                                        {
                                            openwindowordoor = true;                                    
                                        }
                                    }
                                    else
                                    {
                                        if( (!somebodyispresent) && (datapoint.Value()) )
                                        {                                                               if(debug){WriteLine("\t Somebody is present in room:"#datapoint.Value());}
                                            somebodyispresent = true;
                                        }
                                    }
                                }
                            }
                        }                           
                    }
                }
            }
        }
    }
    
    if(debug){ WriteLine("\tThermostats:"#thermostatdevices#" Somebodyispresent in room:"#somebodyispresent#"\topen window or door:"#openwindowordoor);}
    
    if( thermostatdevices.Length() )
    {
        real    newtemp     = miniumroomtemp;
        real    lasttemp    = miniumroomtemp;

        if( openwindowordoor )
        {
            newtemp     = openwindowtemp;
            lasttemp    = openwindowtemp;
        }
        else
        {
            string  roomtemperatureset;
            foreach(roomtemperatureset,roomtemperatures)
            {
                if( roomtemperatureset.Length() > 0 )
                {                                                                                           if(debug){WriteLine("\t testing room:"#roomtemperatureset);}
                    string  roomnameset =   roomtemperatureset.StrValueByIndex(",",0).ToString();           
                    
                    if( roomnameset == room )
                    {                                                                                       if(debug){WriteLine("\t found room:"#roomtemperatureset);}
                        integer newtime     =   roomtemperatureset.StrValueByIndex(",",1).ToInteger();
                        real    newtempset  =   roomtemperatureset.StrValueByIndex(",",2).ToFloat();
                        integer lasttime    =   roomtemperatureset.StrValueByIndex(",",3).ToInteger();
                        real    lasttempset =   roomtemperatureset.StrValueByIndex(",",4).ToFloat();
                        
                        if( newtempset > newtemp )
                        {
                            newtemp     = newtempset;
                            lasttemp    = lasttempset;
                        }
                    }
                }
            }
            if( somebodyispresent && (newtemp<compforttemp) )
            {                                                                                               if(debug){WriteLine("\t somebodyispresent override minimumroomtemp");}
                newtemp     = compforttemp;
                lasttemp    = compforttemp;
            }
        }

        if(debug){WriteLine("\n newtemp:"#newtemp#"\t lasttemp"#lasttemp);}
        
        ! modes
        ! 0 = auto
        ! 1 = manu
        ! 2 = party
        ! 3 = boost

        string  thermostatname;
        foreach(thermostatname,thermostatdevices)
        {                                                                                                   if(debug){WriteLine("thermostatname: "#thermostatname);}
            integer currentmode = dom.GetObject(thermostatname#".CONTROL_MODE").Value();                    if(debug){WriteLine("\tCurrent mode: "#currentmode);}
            
            if( 0 == currentmode )
            {
                real    termostattempcurrent    = dom.GetObject(thermostatname#".SET_TEMPERATURE").Value();         if(debug){WriteLine("\t settemp: "#termostattempcurrent);}
                real    termostattemplast       = dom.GetObject(thermostatname#".SET_TEMPERATURE").LastValue();     if(debug){WriteLine("\t termostattemplast: "#termostattemplast);}

                real    newsettemp              = termostattempcurrent;

                if( lasttemp == termostattemplast )
                {                                                                                           if(debug){WriteLine("lasttemp == termostattemplast: "#lasttemp);}
                    newsettemp = newtemp;
                }
                newsettemp = newtemp;

                if( newsettemp != termostattempcurrent )
                {                                                                                           if(debug){WriteLine("Would change Temperature to: "#newsettemp);}
                    if(!debug) { dom.GetObject(thermostatname#".SET_TEMPERATURE").State(newsettemp);}
                }
            }
        }
    }
} 




