! https://github.com/jollyjinx/homematic/blob/master/ThermostatModeSwitch.hms
!
! Script changes all Thermostats to either auto or manual mode.
!
!----- User setable variables


string  thermostatmodeswitch    = "BidCos-RF.KEQ0768205:4"; ! switch that decides if thermostats should be manual (off) or in auto mode (on)
boolean debug                   = false;                    ! if set, output what is done

!------ Execution

var     modeswitch              = dom.GetObject(thermostatmodeswitch#".STATE");
boolean automode                = modeswitch.Value();                                                           if(debug){WriteLine("modeswitch State:"#automode);}


string  deviceid;
foreach(deviceid, dom.GetObject(ID_DEVICES).EnumUsedIDs()) 
{
    var device  = dom.GetObject(deviceid);                                                                      if(debug){WriteLine("Device:"#device#" (id:"#deviceid#")");}
                                                    
    if( "HM-CC-RT-DN" == device.HSSID() )
    {   
        string channelid;                                                                                                           
        foreach(channelid,device.Channels().EnumUsedIDs())
        {
            var channel = dom.GetObject(channelid);                                                             if(debug){WriteLine("\t Channel:"#channel#" (id:"#channelid#")");}
            
            if( "CLIMATECONTROL_RT_TRANSCEIVER" == channel.HSSID() )
            {
                var     interface   =   dom.GetObject(channel.Interface());
                var     datapoint   =   interface#"."#channel.Address();                                        if(debug){WriteLine("\t Datapoint:"#datapoint);}
                integer currentstate=  dom.GetObject(datapoint#".CONTROL_MODE").Value();                        if(debug){WriteLine("\t State before:"#currentstate);}
                
                if( automode )
                {
                    if( 0!=currentstate)
                    {
                        dom.GetObject(datapoint#".AUTO_MODE").State(1);                                         if(debug){WriteLine("\t Setting to auto mode");}
                    }
                }
                else 
                {
                    if( 1!=currentstate )
                    {
                        dom.GetObject(datapoint#".MANU_MODE").State(0.0);                                       if(debug){WriteLine("\t Setting to manual mode");}
                    }
                }
                                                                                                                if(debug){WriteLine("\t State after:"#dom.GetObject(datapoint#".CONTROL_MODE").Value());}
            }
        }
    }
}   

